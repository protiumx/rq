// HTTP file contains requests or line breaks
file = { SOI ~ (separator | request)* ~ EOI}

request = {	
	request_line ~
    headers? ~
    NEWLINE ~
    body?
}

separator = { "#"{3} ~ NEWLINE+ }

request_line = _{ method ~ " "+ ~ uri ~ " "+ ~ "HTTP/" ~ version ~ NEWLINE }
// URLs can contain any char except for white spaces
uri = { (!whitespace ~ ANY)+ }
method = { ("GET" | "DELETE" | "POST" | "PUT") }
version = { (ASCII_DIGIT | ".")+ }

headers = { header+ }
header = { header_name ~ ":" ~ whitespace+ ~ header_value+ ~ NEWLINE }
header_name = { (!(":" | NEWLINE) ~ ANY)+ }
header_value = { (!NEWLINE ~ ANY)+ }

body = { (!separator ~ ANY)+ }

whitespace = _ { " " | "\t" }
